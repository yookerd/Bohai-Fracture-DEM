; fname: crack_num.p2fis
;
; Simple environment to track fragmentation in a BPM,CBM,FJM,SJM(PFC 2D).
; Track bond_break events from linearcbond, linearpbond, flatjoint, and smoothjoint models, and record them as fractures.
; Use Fragment logic and Ball Result logic to record fragment IDs.

;==============================================================================

define add_crack(entries)
    local contact = entries(1)
    
    contact_model = contact.model(contact)
    if contact_model = 'flatjoint' then
        mode      = entries(3)
    else
        mode      = entries(2)
    endif
    
    local frac_pos   = contact.pos(contact)
    local norm       = contact.normal(contact)
    local dfn_label  = 'crack'
    local frac_size
    local bp1 = contact.end1(contact)
    local bp2 = contact.end2(contact)
    local ret = math.min(ball.radius(bp1),ball.radius(bp2));contact.method(contact,'pb_radius')
    frac_size = ret
    local inDir = vector(-comp.y(norm),comp.x(norm))
    local vert1 = frac_pos + inDir * frac_size
    local vert2 = frac_pos - inDir * frac_size
    local arg = array.create(4)
    arg(1) = 'vertices'
    arg(2) = 2
    arg(3) = vert1
    arg(4) = vert2
    
    ;crack_num = crack_num + 1
    if mode = 1 then 
        ; failed in tension
        ;dfn_label = dfn_label + '_tension'
        ;crack_tension_num = crack_tension_num + 1
        
        dfn_label = 'crack_tension' + '_' + contact_model
        if contact_model = 'linearcbond' then
            crack_tension_num_linearcbond = crack_tension_num_linearcbond + 1
        else if contact_model = 'linearpbond' then
            crack_tension_num_linearpbond = crack_tension_num_linearpbond + 1
        else if contact_model = 'flatjoint' then
            crack_tension_num_flatjoint = crack_tension_num_flatjoint + 1
        else if contact_model = 'smoothjoint' then
            crack_tension_num_smoothjoint = crack_tension_num_smoothjoint + 1
        endif
        crack_tension_num =   crack_tension_num_linearcbond ...
                            + crack_tension_num_linearpbond ...
                            + crack_tension_num_flatjoint ...
                            + crack_tension_num_smoothjoint
    else if mode = 2 then
        ; failed in shear
        ;dfn_label = dfn_label + '_shear'
        ;crack_shear_num = crack_shear_num + 1
        
        dfn_label = 'crack_shear' + '_' + contact_model
        if contact_model = 'linearcbond' then
            crack_shear_num_linearcbond = crack_shear_num_linearcbond + 1
        else if contact_model = 'linearpbond' then
            crack_shear_num_linearpbond = crack_shear_num_linearpbond + 1
        else if contact_model = 'flatjoint' then
            crack_shear_num_flatjoint = crack_shear_num_flatjoint + 1
        else if contact_model = 'smoothjoint' then
            crack_shear_num_smoothjoint = crack_shear_num_smoothjoint + 1
        endif
        crack_shear_num =   crack_shear_num_linearcbond ...
                          + crack_shear_num_linearpbond ...
                          + crack_shear_num_flatjoint ...
                          + crack_shear_num_smoothjoint
    endif
    crack_num = crack_tension_num + crack_shear_num
        
    global dfn = dfn.find(dfn_label)
    if dfn = null then
        dfn = dfn.add(0,dfn_label)
    endif
    local fnew = dfn.addfracture(dfn,arg)
    dfn.fracture.prop(fnew,'failure_strength') = strength
    dfn.fracture.prop(fnew,'failure_age')  = mech.age
    dfn.fracture.extra(fnew,1) = bp1
    dfn.fracture.extra(fnew,2) = bp2
		if crack_num == 1
	        fissureFirst = dfn.fracture.find(1)
		    fissureEnd1 = dfn.fracture.extra(fissureFirst,1)
            fissureEnd2 = dfn.fracture.extra(fissureFirst,2)
		    fissureNorm = dfn.fracture.normal(fissureFirst) 
            ;fissure_Initiation_Angle =  user.vector.dip(fissureFirst)
        endif
    crack_accum += 1
    if crack_accum > 50
        if frag_time < mech.age
            frag_time = mech.age
            crack_accum = 0
            command 
                fragment compute
            endcommand
            ; go through and update the fracture positions
            ;loop for (local i = 0, i < 2, i = i + 1)
            ;    name = 'crack_tension'
            ;    if i = 1
            ;        name = 'crack_shear'
            ;    endif
            ;    dfn = dfn.find(name)
                
            loop foreach dfn dfn.list
                if dfn # null
                    dfn_name = dfn.name(dfn)
                    dfn_name_head = string.sub(dfn_name, 1, 5)
                    if dfn_name_head = 'crack' then
                        loop foreach local frac dfn.fracturelist(dfn)
                            local ball1 = dfn.fracture.extra(frac,1)
                            local ball2 = dfn.fracture.extra(frac,2)
                            if ball1 # null
                                if ball2 # null
                                    local len = dfn.fracture.len(frac)/2.0
                                    local pos = (ball.pos(ball1)+ball.pos(ball2))/2.0
                                    if comp.x(pos)-len > xmin
                                        if comp.x(pos)+len < xmax
                                            if comp.y(pos)-len > ymin
                                                if comp.y(pos)+len < ymax
                                                    dfn.fracture.pos(frac) = pos
                                                endif
                                            endif
                                        endif
                                    endif
                                endif
                            endif
                        endloop
                    endif
                endif
            endloop
        endif
    endif
end

define track_init
    ;loop foreach local dfn dfn.list
    ;    dfn.delete(dfn)
    ;endloop
    command
        ;dfn delete
        ball result clear
        fragment clear
        fragment register ball-ball
    endcommand
  ; activate fishcalls
    command
        ;set fish callback bond_break remove @add_crack
        set fish callback bond_break @add_crack

    endcommand
    ; reset global variables
    global crack_accum = 0
    
    global crack_num = 0
    global crack_tension_num = 0
    global crack_shear_num = 0
    
    global crack_tension_num_linearcbond = 0
    global crack_tension_num_linearpbond = 0
    global crack_tension_num_flatjoint   = 0
    global crack_tension_num_smoothjoint = 0
    
    global crack_shear_num_linearcbond = 0
    global crack_shear_num_linearpbond = 0
    global crack_shear_num_flatjoint   = 0
    global crack_shear_num_smoothjoint = 0
    
    global track_time0 = mech.age
    global frag_time = mech.age
    global xmin = domain.min.x()
    global ymin = domain.min.y()
    global xmax = domain.max.x()
    global ymax = domain.max.y()

end

;==============================================================================
; eof: fracture.p3fis